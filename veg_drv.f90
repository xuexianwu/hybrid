!======================================================================!
subroutine veg_set_cell
!----------------------------------------------------------------------!
! For local position:
! calculate snow masking depth (m);
!----------------------------------------------------------------------!
use constants
use control_parameters
use soil_variables
use veg_variables
!----------------------------------------------------------------------!
implicit none
real*8 :: aa
!----------------------------------------------------------------------!
! Scalar for LAI.
!----------------------------------------------------------------------!
real*8 :: alaic
!----------------------------------------------------------------------!
! Root fraction in layer nsoil_layer (1=fr(1)+fr(2)+...+fr(n))
! (fraction).
!----------------------------------------------------------------------!
do nsoil_layer = 1, nsoil_layers_max
  fr (nsoil_layer) = afr (nsoil_layer,i0,j0)
enddo
!----------------------------------------------------------------------!
! Vegetation height (m).
!----------------------------------------------------------------------!
vh = avh (i0,j0)
!----------------------------------------------------------------------!
! Snow masking depth (m water equivalent).
!----------------------------------------------------------------------!
snowm = vh * spgsn
!----------------------------------------------------------------------!
! Leaf area index of vegetated fraction of cell (m2/m2).
!----------------------------------------------------------------------!
alai = ala (1,i0,j0) + cosday * ala (2,i0,j0) + sinday * ala (3,i0,j0)
alai = max (alai,1.0d0)
alaic = 5.0d0
alaie = alaic * (1.0d0 - exp (-alai / alaic))
!----------------------------------------------------------------------!
! Leaf area index of each plant type.
!----------------------------------------------------------------------!
do kplant = 1, nplant
  alaif (kplant,i0,j0) =          alaf (1,kplant,i0,j0) + &
  &                      cosday * alaf (2,kplant,i0,j0) + &
  &                      sinday * alaf (3,kplant,i0,j0)
  alait (kplant) = alaif (kplant,i0,j0)
enddo
!----------------------------------------------------------------------!
! Minimum canopy stomatal resistance (m/s).
!----------------------------------------------------------------------!
rs = alai / (acs (1,i0,j0) + cosday * acs (2,i0,j0) + &
&    sinday * acs(3,i0,j0))
!----------------------------------------------------------------------!
! Canopy water holding capacity (m).
!----------------------------------------------------------------------!
ws (0,2) = 0.0001d0 * alai
!----------------------------------------------------------------------!
! Heat capacity of the canopy (J/m^2 degC).
!----------------------------------------------------------------------!
aa = ala (1,i0,j0)
shc (0,2) = (0.010d0 + 0.002d0 * aa + 0.001d0 * aa **2) * shw
!----------------------------------------------------------------------!
! Canopy surface water vapour mixing ratio (kg/kg).
!----------------------------------------------------------------------!
qf = qfol_ij (i0,j0)
!----------------------------------------------------------------------!
! Leaf internal CO2 (mol/m^3).
!----------------------------------------------------------------------!
veg_ci = veg_ci_ij (i0,j0)
!----------------------------------------------------------------------!
! Vegetation C pools (kgC/m^2).
!----------------------------------------------------------------------!
 cland (:) = cm (i0,j0,:)
!----------------------------------------------------------------------!
end subroutine veg_set_cell
!======================================================================!
subroutine veg_save_cell
!----------------------------------------------------------------------!
use control_parameters
use veg_variables
!----------------------------------------------------------------------!
implicit none
!----------------------------------------------------------------------!
! Foliage surface water vapour specific humidity (kg/kg).
!----------------------------------------------------------------------!
qfol_ij (i0,j0) = qf
!----------------------------------------------------------------------!
! Internal leaf CO2 (mol/m^3).
!----------------------------------------------------------------------!
veg_ci_ij    (i0,j0) = veg_ci
!----------------------------------------------------------------------!
! Carbon pools (kgC/m^2).
!----------------------------------------------------------------------!
 cm (i0,j0,:) = cland (:)
!----------------------------------------------------------------------!
end subroutine veg_save_cell
!======================================================================!
